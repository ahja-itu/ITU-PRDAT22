%{
  (* File Expr/ExprPar.fsy
     Parser specification for the simple expression language.
   *)

  open Absyn
%}

%token <int> CSTINT
%token <string> NAME
%token PLUS MINUS TIMES DIVIDE EQ
/* Exercise 3.7 - start block */
%token ELSE THEN IF
/* Exercise 3.7 - end block */
%token END IN LET
%token LPAR RPAR
%token EOF

/* Exercise 3.7 - start block */
%right IF THEN ELSE
/* Exercise 3.7 - end block */
%left MINUS PLUS        /* lowest precedence  */
%left TIMES             /* highest precedence */

%start Main
%type <Absyn.expr> Main 

%%

Main:
    Expr EOF                            { $1                }
;

Expr:
    NAME                                { Var $1            }
  | CSTINT                              { CstI $1           }
  | MINUS CSTINT                        { CstI (- $2)       }
  | LPAR Expr RPAR                      { $2                }
  /* Exercise 3.7 - start block */
  | IF Expr THEN Expr ELSE Expr         { If($2, $4, $6)    }
  /* Exercise 3.7 - end block */
  | LET NAME EQ Expr IN Expr END        { Let($2, $4, $6)   }
  | Expr TIMES Expr                     { Prim("*", $1, $3) }
  | Expr PLUS  Expr                     { Prim("+", $1, $3) }  
  | Expr MINUS Expr                     { Prim("-", $1, $3) } 
;
